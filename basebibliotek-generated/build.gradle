// How Jar files are named
group 'no.nb.basebibliotek.generated'
version '1.0-SNAPSHOT'

project.ext {
    jaxbTargetDir = file("$buildDir/generated/sources/xjc")
}

sourceSets.main.java.srcDirs += jaxbTargetDir

configurations {
    jaxb
}

dependencies {
    implementation libs.jaxb.runtime
    jaxb libs.bundles.jaxb.group
}

tasks.register('xsd2java') {
    doLast {
        jaxbTargetDir.mkdirs()
        ant.taskdef(name: 'xjc', classname: 'com.sun.tools.xjc.XJC2Task', classpath: configurations.jaxb.asPath)
        ant.jaxbTargetDir = jaxbTargetDir
        ant.xjc(
                destdir: '${jaxbTargetDir}',
                package: 'no.nb.basebibliotek.generated',
                schema: 'https://www.nb.no/basebibliotek/resources/basebibliotek.xsd'
        )
    }
}

tasks.withType(JavaCompile).configureEach {
    dependsOn(xsd2java)
}
