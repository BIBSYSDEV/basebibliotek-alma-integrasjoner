// How Jar files are named
group 'no.sikt.alma.partners.generated'
version '1.0-SNAPSHOT'

project.ext {
    jaxbTargetDir = file("${layout.buildDirectory.asFile.get()}/generated/sources/xjc")
}

sourceSets.main.java.srcDirs += jaxbTargetDir

configurations {
    jaxb
}

dependencies {
    implementation libs.jaxb.runtime
    implementation libs.jakarta.xml.bind
    jaxb libs.bundles.jaxb.group
}

tasks.register('xsd2javaPartners') {
    doLast {
        jaxbTargetDir.mkdirs()
        ant.taskdef(name: 'xjc', classname: 'com.sun.tools.xjc.XJC2Task', classpath: configurations.jaxb.asPath)
        ant.jaxbTargetDir = jaxbTargetDir
        ant.xjc(
                destdir: '${jaxbTargetDir}',
                package: 'no.sikt.alma.partners.generated',
                schema: 'https://developers.exlibrisgroup.com/wp-content/uploads/alma/xsd/rest_partners.xsd'
        )
    }
}

tasks.register('xsd2javaUser') {
    doLast {
        jaxbTargetDir.mkdirs()
        ant.taskdef(name: 'xjc', classname: 'com.sun.tools.xjc.XJC2Task', classpath: configurations.jaxb.asPath)
        ant.jaxbTargetDir = jaxbTargetDir
        ant.xjc(
                destdir: '${jaxbTargetDir}',
                package: 'no.sikt.alma.user.generated',
                schema: 'https://developers.exlibrisgroup.com/wp-content/uploads/alma/xsd/rest_user.xsd'
        )
    }
}

tasks.withType(JavaCompile).configureEach {
    dependsOn(xsd2javaPartners)
    dependsOn(xsd2javaUser)
}
