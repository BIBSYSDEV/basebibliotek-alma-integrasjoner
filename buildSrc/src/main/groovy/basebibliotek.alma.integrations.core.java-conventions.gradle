plugins {
    // Plugins that applies to all modules
    // Java Library: All things java
    id 'java-library'
    // Jacoco: Test coverage plugin
    id 'jacoco' // Can we skip this for auto generated modules?
    // Shadow: Generate fat jar
    id 'com.github.johnrengelman.shadow'
    // Nebula Lint: Linting gradle build files
    id 'nebula.lint' // Move to subprojects excluding auto generated ones?
    id 'jacoco-report-aggregation' // Can we skip this for auto generated modules?
}

repositories {
    mavenCentral()
    // Jitpack: User submitted repositories
    maven { url 'https://jitpack.io' }
}

subprojects {
    subproject ->
        if (!subproject.name.endsWith("-generated")) {
            // Applies to all modules excluding those that are auto generated by schema files
            apply plugin: 'pmd'
            apply plugin: 'checkstyle'

            // Setup PMD
            pmd {
                ruleSetConfig = rootProject.resources.text.fromFile('config/pmd/ruleset.xml')
                ruleSets = []
                ignoreFailures = false
            }

            // Setup Checkstyle
            checkstyle {
                configFile = rootProject.resources.text.fromFile('config/checkstyle/checkstyle.xml').asFile()
                configProperties.checkstyleSuppressionsPath =
                        rootProject.resources.text
                                .fromFile('config/checkstyle/checkstyle-suppressions.xml').asFile()
                showViolations = true
            }

            // We do not want a jar for our own modules, just a fatJar.
            // For all auto generated modules, we need a jar that contains the classes or else the build will fail
            // Therefore we only exclude jar generation in this code block
            jar.enabled = false
        }
}

// Running tests using gradle
tasks.named('test', Test) {
    useJUnitPlatform()
    failFast = true
    finalizedBy jacocoTestReport
    testLogging {
       events 'skipped', 'failed' //, 'passed'
    }
}

// We want to compile all jars including those from auto generated modules before building or else the build will fail
tasks.named('build').configure {
    dependsOn tasks.named('shadowJar')
}

// Test coverage verification rules
tasks.named('jacocoTestCoverageVerification', JacocoCoverageVerification) {
    violationRules {
        rule {
            limit {
                counter = 'METHOD'
                value = 'COVEREDRATIO'
                minimum = 0.9
            }
        }

        rule {
            limit {
                counter = 'CLASS'
                value = 'COVEREDRATIO'
                minimum = 1.0
            }
        }
    }
    dependsOn jacocoTestReport
}

// Generate html checkstyle report using xsl style file excluding generated modules
tasks.withType(Checkstyle).configureEach{
    exclude('*generated')
    reports {
        html.required
        html.stylesheet rootProject.resources.text.fromFile('config/checkstyle/checkstyle-simple.xsl')
    }
}

// Lint dependencies on build
gradleLint {
    rules = ['all-dependency']
    reportFormat = 'html'
    reportOnlyFixableViolations = true
}

// Generate html coverage report in reports directory
jacocoTestReport {
    reports {
        html.destination file("${buildDir}/reports/jacoco")
    }
}

// Use utf-8 for compilation
tasks.withType(JavaCompile).configureEach {
    options.encoding = 'utf-8'
}

// Verify test coverage is over minimum required values
tasks.named('check').configure {
    dependsOn tasks.named('jacocoTestCoverageVerification')
}
