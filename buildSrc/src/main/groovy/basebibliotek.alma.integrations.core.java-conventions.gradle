plugins {
    // Applies to all modules
    id 'java-library'
    id 'java'
    id 'jacoco' // Can we skip this for auto generated modules?
    id 'com.github.johnrengelman.shadow'
    id 'nebula.lint' // Move to subprojects excluding auto generated ones?
    id 'jacoco-report-aggregation' // Can we skip this for auto generated modules?
}

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
}

subprojects {
    subproject ->
        if (!subproject.name.endsWith("-generated")) {
            // Applies to all modules excluding those that are auto generated by schema files
            apply plugin: 'pmd'
            apply plugin: 'checkstyle'

            pmd {
                ruleSetConfig = rootProject.resources.text.fromFile('config/pmd/ruleset.xml')
                ruleSets = []
                ignoreFailures = false
            }

            checkstyle {
                configFile = rootProject.resources.text.fromFile('config/checkstyle/checkstyle.xml').asFile()
                configProperties.checkstyleSuppressionsPath =
                        rootProject.resources.text
                                .fromFile('config/checkstyle/checkstyle-suppressions.xml').asFile()
                showViolations = true
            }
        }
}

tasks.named('test', Test) {
    useJUnitPlatform()
    failFast = true
    finalizedBy jacocoTestReport
    testLogging {
       events 'skipped', 'failed' //, 'passed'
    }
}

// We don't want a jar, just a fatJar
jar.enabled = false
project.tasks.build.dependsOn project.tasks.shadowJar

// TODO: Coverage verification does not seem to work. Fix
tasks.named('jacocoTestCoverageVerification', JacocoCoverageVerification) {
    violationRules {
        rule {
            limit {
                counter = 'METHOD'
                value = 'COVEREDRATIO'
                minimum = 1.0
            }
        }

        rule {
            limit {
                counter = 'CLASS'
                value = 'COVEREDRATIO'
                minimum = 1.0
            }
        }
    }
    dependsOn jacocoTestReport
}

tasks.withType(Checkstyle).configureEach{
    exclude('*generated')
    reports {
        html.required
        html.stylesheet rootProject.resources.text.fromFile('config/checkstyle/checkstyle-simple.xsl')
    }
}

gradleLint {
    rules = ['all-dependency']
    reportFormat = 'html'
    reportOnlyFixableViolations = true
}

jacocoTestReport {
    reports {
        html.destination file("${buildDir}/reports/jacoco")
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'utf-8'
}
