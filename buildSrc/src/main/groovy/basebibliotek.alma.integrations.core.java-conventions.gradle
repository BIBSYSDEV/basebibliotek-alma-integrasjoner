plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow'
    id 'nebula.lint'
    id 'jacoco-report-aggregation'
}

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
}

subprojects {
    subproject ->
        if (!subproject.name.endsWith("generated")) {
            subproject.pluginManager.apply('pmd')
            subproject.pluginManager.apply('checkstyle')
            subproject.pluginManager.apply('jacoco')


            pmd {
                toolVersion = '7.14.0'
                ruleSetConfig = rootProject.resources.text.fromFile('config/pmd/ruleset.xml')
                ruleSets = []
                ignoreFailures = false
            }

            checkstyle {
                toolVersion = '10.17.0'
                configFile = rootProject.resources.text.fromFile('config/checkstyle/checkstyle.xml').asFile()
                configProperties.checkstyleSuppressionsPath =
                        rootProject.resources.text
                                .fromFile('config/checkstyle/checkstyle-suppressions.xml').asFile()
                showViolations = true
            }

            jacoco {
                toolVersion = '0.8.11'
            }

            jacocoTestReport {
                reports {
                    html.outputLocation.set(layout.buildDirectory.dir("reports/jacoco"))
                }
            }

            // We do not want a jar for our own modules, just a fatJar.
            jar.enabled = false
        }
}

sourceSets {
    test {
        resources {
            exclude '**/*.java' , '*/*.xml', '*generated'
        }
    }

}
// Running tests using gradle
tasks.named('test', Test) {
    useJUnitPlatform()
    failFast = true
    finalizedBy jacocoTestReport
    testLogging {
       events 'skipped', 'failed' //, 'passed'
    }
}

// We want to compile all jars especially those from auto generated modules before building or else the build will fail
tasks.named('build').configure {
    dependsOn tasks.named('shadowJar')
}

tasks.named('testCodeCoverageReport', JacocoReport) {

    reports {
        html.required.set(true)
        xml.required.set(false)
        csv.required.set(false)
    }
}

tasks.named('jacocoTestCoverageVerification', JacocoCoverageVerification) {
    violationRules {
        rule {
            excludes = [
                    '*generated'
            ]
            limit {
                counter = 'METHOD'
                value = 'COVEREDRATIO'
                minimum = 0.9
            }
        }

        rule {
            excludes = [
                    '*generated'
            ]
            limit {
                counter = 'CLASS'
                value = 'COVEREDRATIO'
                minimum = 1.0
            }
        }
    }
    dependsOn jacocoTestReport
}

tasks.withType(Checkstyle).configureEach{
    exclude('*generated')
    reports {
        html.required
        html.stylesheet rootProject.resources.text.fromFile('config/checkstyle/checkstyle-simple.xsl')
    }
}

// Lint dependencies on gradle build using nebula lint
gradleLint {
    rules = ['all-dependency']
    reportFormat = 'html'
    reportOnlyFixableViolations = true
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'utf-8'
}

// Verify test coverage is over minimum required values
tasks.named('check').configure {
    dependsOn tasks.named('jacocoTestCoverageVerification')
}
